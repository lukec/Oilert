#!/usr/bin/env perl
use strict;
use warnings;
use URI;
use Web::Scraper;
use YAML qw/DumpFile/;

my $ship_scraper = scraper {
    process "table tr", "ships[]" => scraper {
        process "td:nth-child(2)", 'name' => 'TEXT';
        process "td:nth-child(2) a", 'detail_url' => '@href';
        process "td:nth-child(3)", 'type' => 'TEXT';
        process "td:nth-child(4)", 'speed' => 'TEXT';
        process "td:nth-child(11) a", 'map_url' => '@href';
    };
    result 'ships';
};

my %data;
for my $type (qw/Cargo Tanker/) {
    my $uri = "http://www.marinetraffic.com/ais/datasheet.aspx?TYPE_SUMMARY=$type&PORT_ID=682&datasource=SHIPS_CURRENT&B1=Search";
    my $ships = $ship_scraper->scrape(URI->new($uri)) || [];
    $ships = [ grep { keys %$_ and not $_->{speed} } @$ships ];
    for my $s (@$ships) {
        for (qw/map_url detail_url/) {
            $s->{$_} = $s->{$_}->as_string;
        }
        if ($s->{detail_url} =~ m/MMSI=(\d+)/) {
            $s->{mmsi} = $1;
        }
        if ($s->{map_url} =~ m/centerx=([-\d.]+)&centery=([\d\.]+)/) {
            $s->{longitude} = $1;
            $s->{latitude} = $2;
        }
    }
    $data{$type} = $ships;
}

DumpFile('data/ships.yaml', \%data);
